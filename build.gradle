plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
    id "org.ajoberstar.grgit" version "3.1.1"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.2.0"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "com.github.ben-manes.versions" version "0.42.0"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + (project.hasProperty("devbuild") ? ("-" + project.findProperty("devbuild")) : "")
group = project.maven_group

repositories {
    mavenLocal()
    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    maven {
		name = "meteor-maven"
		url = "https://maven.meteordev.org/releases"
	}
    maven {
        name = "meteor-maven-snapshots"
        url = "https://maven.meteordev.org/snapshots"
    }
    maven {
        name = "vram"
        url = "https://maven.vram.io//"
    }

    maven {
        name = "seedfinding-maven"
        url "https://maven.seedfinding.com/"
    }
    maven {
        name = "seedfinding-maven-snapshots"
        url "https://maven-snapshots.seedfinding.com/"
    }
    maven { url 'https://jitpack.io' }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.latticg.com/" }
    maven { url 'https://repo.viaversion.com/' }
    //maven { url = "https://maven.legacyfabric.net/" }
    maven { url = "https://maven.terraformersmc.com/releases/" }
    mavenCentral()
}

dependencies {
	def library = { id ->
		implementation(id) { exclude group: "org.slf4j" }
		shadow(id) { exclude group: "org.slf4j" }
	}

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.clothconfigfabric_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include("me.shedaniel.cloth:cloth-config-fabric:7.0.65")


	// Fabric
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Compat fixes
    modCompileOnly fabricApi.module("fabric-renderer-indigo", project.fapi_version)
    modCompileOnly("me.jellysquid.mods:sodium-fabric:${project.sodium_version}") { transitive = false }
    modCompileOnly("io.vram:canvas-fabric-mc118:1.0.+") { transitive = false }

	// Baritone (https://github.com/MeteorDevelopment/baritone)
    modImplementation "baritone:fabric:${project.minecraft_version}-SNAPSHOT"
    include "baritone:fabric:${project.minecraft_version}-SNAPSHOT"

   implementation (include("me.shedaniel.cloth:cloth-config-fabric:${project.clothconfigfabric_version}")) {transitive = false}


    // seed .locate and ore sim
    library('com.seedfinding:mc_math:LATEST')
    library('com.seedfinding:mc_seed:LATEST')
    library('com.seedfinding:mc_core:LATEST')
    library('com.seedfinding:mc_noise:LATEST')
    library('com.seedfinding:mc_biome:LATEST')
    library('com.seedfinding:mc_terrain:LATEST')
    library('com.seedfinding:mc_feature:LATEST')
    library('com.seedfinding:mc_reversal:LATEST')
    library('com.seedfinding:latticg:1.06')

    // seedcracker api
    implementation (include('com.github.19MisterX98.SeedcrackerX:seedcrackerx-api:2.10.1')) {transitive = false}

    // meteor gl render
    implementation (include("com.github.ben-manes.caffeine:caffeine:${caffeine_version}")) {transitive = false}
    implementation (include("com.vladsch.flexmark:flexmark:${flexmark_version}")) {transitive = false}
    implementation (include("io.reactivex.rxjava3:rxjava:${rxjava_version}")) {transitive = false}
    //viaversion
    implementation (include("com.viaversion:viaversion:${project.viaversion_version}")) {transitive = false}
    implementation (include("com.viaversion:viarewind-all:${project.viarewindall_version}")) {transitive = false}
    implementation (include("com.viaversion:viabackwards:${project.viabackwards_version}")) {transitive = false}
    implementation (include("org.yaml:snakeyaml:${project.snakeyaml_version}")) {transitive = false}

    modImplementation (include("io.github.ladysnake:satin:1.9.0")) {transitive = false}

    // Libraries
    library "meteordevelopment:orbit:${project.orbit_version}"
    library "meteordevelopment:starscript:${project.starscript_version}"
    library "meteordevelopment:discord-ipc:${project.discordipc_version}"
    library "org.reflections:reflections:${project.reflections_version}"

    // Proxies
    implementation("io.netty:netty-handler-proxy:${project.netty_version}") { transitive = false }
    shadow("io.netty:netty-handler-proxy:${project.netty_version}") { transitive = false }

    implementation("io.netty:netty-codec-socks:${project.netty_version}") { transitive = false }
    shadow("io.netty:netty-codec-socks:${project.netty_version}") { transitive = false }



    // Launch sub project
    shadow project(":launch")
}

loom {
    accessWidenerPath = file("src/main/resources/meteor-client.accesswidener")
}

shadowJar {
	configurations = [ project.configurations.shadow ]
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version,
               "devbuild": project.findProperty("devbuild") ?: "",
               "commit": project.findProperty("commit") ?: ""
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding("UTF-8")
}

jar {
	from "LICENSE"

	manifest {
		attributes("Main-Class": "meteordevelopment.meteorclient.Main")
	}
}

remapJar {
	dependsOn shadowJar
	input.set(shadowJar.archiveFile)
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}


publishing {
    publications {
        java(MavenPublication) {
            artifactId = "light-meteor"
            version = project.hasProperty("devbuild") ? (project.mod_version + "-SNAPSHOT") : project.mod_version

            artifact(remapJar) {
                builtBy remapJar
            }

            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            artifact(javadocJar) {
                builtBy javadocJar
            }
        }
    }

    repositories {
        maven {
            name = "meteor-maven"
            url = project.hasProperty("devbuild") ? "https://maven.meteordev.org/snapshots" : "https://maven.meteordev.org/releases"

            credentials {
                username = System.getenv("MAVEN_METEOR_ALIAS")
                password = System.getenv("MAVEN_METEOR_TOKEN")
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}
